# F# Smart Constructor

<!-- [Sebastian] An welche Leserschaft richtet sich der Artikel? Der Artiekl setzt zwar nicht zwingend F#-Kenntnisse voraus, geht allerdings auf einige F#-Punkte nur kurz oder gar nicht ein. Ich vermute, grundlegende F#-Kenntnisse sollte der Leser mitbringen, richtig?  -->

Das "F# Smart Constructor" Pattern erm√∂glicht die Erzeugung von validierten F# Typen.

<!-- [Sebastian] Was ist ist Motivation, hier F# zu verwenden? L√§sst sich mit F# das Pattern besonders gut zeigen? Oder ist es in F# besonders wichtig, dieses Pattern zu verwenden? Oder l√§sst sich die Validierung - anders als in C# - in F# besonders gut umsetzen? -->

## TL;DR

```fsharp
type UserName = private UserName of string

module UserName =
    let isValid s = // ...

    // smart ctor
    let create (str: string) =
        if isValid str then
            Ok (UserName str)
        else
            Error "Invalid user name"

    // helper function extracting value
    let value (UserName s) = s
```

Anwendung:

```fsharp
let maybeValidUserName name = 
    match UserName.create name with
    | Ok validName -> validName |> UserName.value
    | Error e -> e

maybeValidUserName ""     // -> "Invalid user name"
maybeValidUserName "lisa" // -> "lisa"
```

## Einleitung

In diesem Artikel m√∂chte ich zeigen, wie man in F# Typen mit eingebauter Validierung erzeugt.

Beginnen wir mit einem einfachen `Person` Typ:

```fsharp
type Person = {
    FirstName: string
    LastName: string
}
```

Dies ist ein `record` mit einem `FirstName`, und einem `LastName` (Anmerkung: Seit C# 9 gibt es das Konzept von `record`s auch in C#. Und seit C# 10 gibt es auch `record struct`, was dem F# `record` Konstrukt recht nahe kommt). 

Um eine Instanz dieses Typs zu erzeugen, muss jedem Feld ein Wert zugewiesen werden:

<!-- [Sebastian] Woran kann man erkennen, dass dies hier eine Instanz des Typs `Person` ist? F√ºr mich sieht das aus wie ein typenloser Adhoc-Record. -->

```fsharp
let homer = {
    FirstName = "Homer"
    LastName = "Simpson"
}
```

M√∂chte man nun eine Formatierungsfunktion anbieten, die mit dem Vor- und Nachnamen arbeitet, k√∂nnte eine einfache Implementierung folgenderma√üen aussehen:

```fsharp
// string -> string -> string
let formatName firstName lastName = $"{lastName}, {firstName}"
```

<!-- [Sebastian] Guter Hinweis, denn hier ist f√ºr Non F# Reader eine kleine Erl√§uterung der sonderbar aussehenden Syntax n√∂tig (ohne auch Currying eingehen zu m√ºssen). F√ºr einen Python-Leser zum Beispiel sieht das oben aus wie eine Zuweisung an 3 Variablen. -->

Hinweis: Der Kommentar `// string -> string -> string` beschreibt die Typsignatur der darunterstehenden Funktion. Da F# eine bessere Typinferenz als C# hat, muss man meist die Typen nicht explizit angeben. F√ºr diesen Artikel habe ich an manchen Stellen die Signatur als Kommentar hinzugef√ºgt um Unklarheiten zu vermeiden. Der letzte Wert in der Signatur ist der R√ºckgabewert der Funktion. <!-- [Sebastian] Du meinst hier nicht den Wert, sondern den Typ in der Signatur, richtig? --> Alle anderen Werte sind Parameter der Funktion. <!-- [Sebastian] Das k√∂nnte man jetzt so verstehen, als w√§re `lastName` der R√ºckgabewert, was er ja nicht ist, richtig? Der R√ºckgabewert der Funktion ist der Wert des letztens Ausdrucks - wobei es hier in der Funktion nur einen Ausdruck gibt. --> In C# w√§re die entsprechende Signatur `Func<string, string, string>`.

Da es sich beim Vor- und Nachnamen um einen `string` handelt, besteht eine gewisse Verwechslungsgefahr:

```fsharp
// Achtung: Vor- und Nachname sind vertauscht:
let formattedName = formatName "Simpson" "Homer"
```

<!-- [Sebastian] Kurzer Hinweis, dass das hier die Anwendung der zuvor definierten Funktion `formatName` ist? C#- und Python3-Leser vermissen hier die Klammern - Python2-Leser kommen damit zurecht. -->

Dieses Problem wird auch als ["Primitive Obsession"](https://wiki.c2.com/?PrimitiveObsession) Antipattern bezeichnet: Anstatt das vorhandene Typ-System zu nutzen, wird ein `string` verwendet. Abhilfe schafft die Verwendung dedizierter Typen f√ºr Vor- und Nachname:

<!-- [Sebastian] In Python gibt es dazu *Named Parameters*, hier z√§hlt nur der Name des Parameters, nicht seine Position in der Liste der Argumente. In F#-Sytnax s√§he das dann so aus:
 
```fsharp
let formattedName = formatName firstName="Simpson" lastName="Homer"
``` 
 -->

```fsharp
type FirstName = FirstName of string
type LastName = LastName of string
```

Im Gegensatz zum `Person`-Typ handelt es sich hier um sogenannte [Single-Case Discriminated Unions](https://fsharpforfunandprofit.com/posts/designing-with-types-single-case-dus/).

<!-- [Sebastian] Was ist hier genau eine *Union*? Den Begriff *Wrapper* aus dem verlinkten Artikel verstehe ich noch. -->

Der `Person`-Typ kann nun diese dedizierten Typen verwenden:

```fsharp
type Person = {
    FirstName: FirstName
    LastName: LastName
}
```

<!-- [Sebastian] Und hier f√§ngt mein Hirn an auszusteigen, zumal der der Ansatz ja oben versprochen hat, die Verwechslungsgefahr zu reduzieren. Ich sehe jetzt zwei Felder und zwei Typen, wobei Feld und Typ den gleichen Namen tragen. :-o Ist das √ºbliche F#-Praxis? In anderen Sprachen w√ºrde man statt `type FirstName` vermutlich eher `type FirstNameType` schreiben, um diese Verwechslung zu vermeiden? -->

Um nun eine Instanz dieses Typs zu erzeugen, muss der jeweilige `string` erst in den entsprechenden `FirstName`- und `LastName`-Typen umgewandelt werden:

```fsharp
let bart = {
    FirstName = FirstName "Bart"
    LastName = LastName "Simpson"
}
```

<!-- [Sebastian] Dieses Zeilen oben sehen f√ºr mich jetzt kurios aus, `FirstName` auf der linken und auf der rechten Seite des Zuweisungsoperators. -->

Der `formatName`-Aufruf kann nun ohne Verwechslungsgefahr der Parameterreihenfolge erfolgen, da eine falsche Reihenfolge vom Compiler unterbunden wird:

```fsharp
let formattedName1 = formatName bart.LastName bart.FirstName // -> "Simpson, Bart"
let formattedName2 = formatName (FirstName "Bart") (LastName "Simpson") // -> "Simpson, Bart"
let formattedName3 = formatName (LastName "Simpson") (FirstName "Bart") // üò° kompiliert nicht
```

<!-- [Sebastian] Frage am Rande: Ist das Problem nicht eher ein gestelltes? Sollte die Funktion `formatName` nicht besser den gesamten Record √ºbergeben bekommen und sich selber herauspicken, was sie braucht? Nun, ich verstehe, es geht hier um Lehrzwecke... Die Funktion k√∂nnte ja auch zwei Records √ºbergeben bekommen und dann h√§tten wir das gleichen Problem wieder. Was mich auf eine interessante Folgefrage bringt. Die beiden `string`-Parameter wrappen, verstehe ich, gekauft. W√ºrde man auf √§hnliche Weise zwei ganze `Person`-Records mappen und daf√ºr dann zwei Wrapper-Typen `Husband` und `Wife` definieren? --->

## Neue Anforderungen

Szenenwechsel: Aus unerkl√§rlichen Gr√ºnden √§ndern sich die Anforderungen f√ºr den `Person`-Typ. Die Angabe eines Vor- und Nachnamens ist nun optional, daf√ºr muss ein `UserName` vorhanden sein. Der `UserName` hat zus√§tzlich die Anforderung, dass er (a) nicht leer sein darf und (b) nicht mehr als 10 Zeichen lang sein darf.

Die Umsetzung von optionalen Eigenschaften in `record`s ist dank des F# `option`-Typs einfach umzusetzen:

```fsharp
type Person = {
    FirstName: FirstName option
    LastName: LastName option
}
```

Hinweis: Auch wenn der `option`-Typ vielleicht auf den ersten Blick wie ein `nullable`-Typ in C# aussieht (`FirstName?`): Nein, das ist nicht das Gleiche. `option`s k√∂nnen miteinander kombiniert werden, sind also um einiges m√§chtiger als C# `nullable`s.

<!-- [Sebastian] K√∂nntest Du auf den `option`-Type noch etwas genauer eingehen? Ich bin hier gestolpert: "Hm, das ist echt ein Typ?" `option` ist in F# wohl wirklich ein eigener Typ, dem ein Typ zugrundeliegt und Werte von eben diesem Typ kann so ein `option`-Wert annehmen oder eben keinen Wert.` Und ja, f√ºr mich klingt das sehr nach einem `nullable`-Typ in C#. Inwiefern `option` jetzt m√§chtiger ist, erschlie√üt sich mir auf die Schnelle nicht - ist vermutlich hier aber auch nicht von Belang, oder? -->

Eine Person kann also auch ohne Vor- und/oder Nachnamen erzeugt werden:

```fsharp
let marge = {
    FirstName = Some (FirstName "Marge")
    LastName = None
}
```

<!-- [Sebastian] K√∂nntest Du zu dem `Some` noch kurz was sagen? Vielleicht die Deklaration von `option` dazu schreiben, dann w√§ren `None` und `Some` gleicherma√üen kurz erl√§uert. Oder setzt Du diese Kenntnisse voraus? -->

Doch wie handhabt man den `UserName` mit Validierung?

```fsharp
type Person = {
    FirstName: FirstName option
    LastName: LastName option
    UserName: UserName // ??
}
```

<!-- [Sebastian] Wie sieht die Deklartion des Tytps `UserName` aus? Das w√§re hier insofern wichtig zu sehen, weil Du ja vermutlich darauf rauswillst, dass die beiden Constraints zum Typ `UserName` im `Person`-Records aufgehiben werden sollen, nicht aber im Typ `UserName`, richtig? Wobei sich die Frage stellt... warum eigentlich im `Person`-Record und nicht im `UserName`-Typ? :-o -->

<!-- [Sebastian] Bis hierhin bin ich heute gekommen. To be continued... :-) -->

## Smart Constructor to the rescue

Hier kommt das "Smart Constructor" Pattern ins Spiel:

```fsharp
// `UserName` is private
type UserName = private UserName of string

module UserName =
    let isValid s = not (String.IsNullOrEmpty(s)) && s.Length < 10

    // smart constructor
    // string -> Result<UserName, string>
    let create (str: string) =
        if isValid str then
            Ok (UserName str)
        else
            Error $"invalid UserName '{str}'"

    // helper function to extract the string
    // UserName -> string
    let value (UserName str) = str
```

Der `UserName`-Typ wird mit dem `private` Attribut versehen. Somit kann der `UserName` nur innerhalb des aktuellen Scopes erzeugt werden.

Im n√§chsten Schritt wird ein gleichnamiges Modul erzeugt, welches eine `create`-Funktion enth√§lt. Innerhalb dieser Funktion wird die Eingabe validiert (`isValid`). Der R√ºckgabewert der `create`-Funktion ist ein `Result`-Typ, welcher entweder einen `Ok UserName` oder `Error msg` enth√§lt.

Hier ein Beispiel, wie die `create`-Funktion aufgerufen wird:

```fsharp
let maybeUserName someString = 
    match UserName.create someString with
    | Ok validName -> validName |> UserName.value
    | Error e -> e

maybeUserName "lisa" // -> "lisa"
maybeUserName ""     // -> "invalid UserName ''"
```

Wichtig: Die `create`-Funktion muss nicht zwingend einen `Result`-Typ zur√ºckgeben wie in diesem Beispiel. Wenn die Eingabe nicht g√ºltig ist, kann die `create`-Funktion auch ein `option`-Typ zur√ºckgeben, eine Exception werfen oder etwas anderes machen! 

Es geht darum, dass es nie einen ung√ºltigen `UserName` geben kann.

Wie mit dem Ergebnis der Validierung umgegangen wird, kann man selbst entscheiden.

Ob man nun einen weiteren Smart Constructor f√ºr den `Person`-Typ verwendet oder eine andere Strategie, wie beispielsweise das "Applicative Validation"-Pattern, steht einem frei.

## Beispiel

Das Erstellen eines `Person`-Typs mittels einer `tryCreatePerson` Funktion k√∂nnte z. B. folgenderma√üen aussehen:

```fsharp
module Demo.Domain
open System

type FirstName = FirstName of string
type LastName = LastName of string

type UserName = private UserName of string
module UserName =
    let isValid s = not (String.IsNullOrEmpty(s)) && s.Length <= 10
    
    // smart constructor
    let create (str: string) =
        if isValid str then
            Ok (UserName str)
        else
            Error $"UserName is invalid: '{str}'."
    
    // helper function to extract the string
    let value (UserName str) = str

type Person = {
    FirstName: FirstName option
    LastName: LastName option
    UserName: UserName
}

// string -> string -> string -> Result<Person, string>
let tryCreatePerson fn ln un =

    // string -> Option<FirstName>    
    let maybeFirstName fn =
        if String.IsNullOrEmpty(fn) then
            None
        else
            Some (FirstName fn)
        
    // string -> Option<LastName>
    let maybeLastName ln =
        if String.IsNullOrEmpty(ln) then
            None
        else
            Some (LastName ln)

    // string -> Result<Person, string>        
    match UserName.create un with
    | Error e ->
        Error e
    | Ok validNameUserName ->
        Ok  {
                FirstName = maybeFirstName fn
                LastName = maybeLastName ln
                UserName = validNameUserName
            }
```

## Fazit

Um sicherzustellen, dass vermeintlich einfache Typen (wie `string`) im Dom√§nenkontext Sinn ergeben ("Optionaler Vorname", "UserName mit Regeln"), bietet es sich an, dedizierte Typen zu erstellen. 

Das "Smart Constructor" Pattern erleichtert die Validierung einfacher Typen, indem das Erstellen ung√ºltiger Typen unterbunden wird (Stichwort: ["Prevent inpossible states (oder wie das heisst TODO Better link"](https://sporto.github.io/elm-patterns/basic/impossible-states.html)).))

Ich m√∂chte hier auch hervorheben, dass das obiges Beispiel sehr kompakt und einfach ist.
Es ist √ºberschaubarer/wartbarer (["it fits in your head"](https://blog.ploeh.dk/2021/06/14/new-book-code-that-fits-in-your-head/)), als das Gegenst√ºck in C#.

## Resources

- Dies ist eine freie √úbersetzung eines privaten Blog-Posts von mir: https://draptik.github.io/posts/2020/02/10/fsharp-smart-constructor/
- Code, zum Rumspielen, und Tests, gibts hier: [Demo](Demo)
